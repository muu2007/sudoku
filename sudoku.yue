import 'macros' as {$}
_G.PROJECTNAME, _G.VERSION = $FILE\sub(1,-5), "0.1a(#{$DATE})"
-- import 'utf8'
import 'lib.lume'
import 'utl' as :align, :play
import 'lib.maid64' -- 改造して高解像度を初期値にした
import 'color' as :hsv, :sweetie16
-- import 'lib.gifcat' -- gifcat.luaもprojectrootに置く? 全部に?入れているので、ここ消すだけで使わなくなる
import 'ui' as :Scene, :Overlay, :SelectBox, :SelectBoxScroll, :Watchdog--, :blur5x5_code
import 'input' as :input, :SoftKeyboard
import 'lib.tween' -- 改造してdelayを可能にした
import '数独' as :solve, :generate--{solve0: solve, generate: generate}
import 'sudoku_stagedata' as stagedata
import 'lib.inspect'
-- local *

love.window.setTitle(PROJECTNAME .. ' - v' .. VERSION)
love.window.setMode(512, 800, {resizable: true}) -- レトロ画面をコピーするだけならanti alias不要。gamepad描いているのでmsaaつけた
maid64.setup(512, 800)
love.mouse.getPosition0 = love.mouse.getPosition -- 保存
love.mouse.getPosition = maid64.mouse.getPosition -- プログラム全体としてmaid64の座標系を使うことにする
love.touch.getPosition0 or= love.touch.getPosition -- 保存
love.touch.getPosition = (id)-> maid64.transformPosition(love.touch.getPosition0(id))
love.audio.setVolume(0.3)
-- os.lang = nil -- i18nを無効
-- if love.system.hasBackgroundMusic() then love.audio.toggleMute() --うまく動いてない
-- BGM'assets/sounds/052622ineedtogetouttahere_32k.mp3'


do -- load the savedata
	savedata = if 'Web' != love.system.getOS() then lume.deserialize(love.filesystem.read(PROJECTNAME..'.save') or '{}') else {}
	stagedata[i].cleartime = t for i, {cleartime: t} in ipairs savedata

save = ->
	if 'Web' == love.system.getOS() then return
	love.filesystem.write(PROJECTNAME..'.save', lume.serialize([{cleartime: t} for {cleartime: t} in *stagedata]))


class Game extends Scene
	new: (@id)=>
		@field = [ [(if 0==c then {} else c) for c in *row] for row in *stagedata[id].field]
		@keycursor = {1,1, visible: false} -- キーボード操作用
		@time = 0 -- Resultでは止めたいのでstartとlove.timer.getTimeではダメ
		super()
		@keyboard = SoftKeyboard({'appback': {50, 50, 28, "◀", 60}, 'f9': {150, 30, 20, "解", 60}, 'm': {220, 30, 20, "音"}})

	@OX, @OY, @W, @H = 6, 130, 500, 500
	getIndexPos: =>
		x, y = love.mouse.getPosition()
		if math.pos_in_box({x, y}, {@@OX, @@OY, @@W, @@H}) -- else では nil, nil が返る
			lume.clamp(((x-@@OX) |>idiv @@W/9)+1, 1, 9), lume.clamp(((y-@@OY) |>idiv @@H/9)+1, 1, 9)

	iswin: =>
		isvalid = (x, y)->
			return false if 9 != #lume.unique([(if 'number' == type c then c else c[1]) for c in *@field[y]])
			return false if 9 != #lume.unique([(if 'number' == type row[x] then row[x] else row[x][1]) for row in *@field])
			sx, sy =  rawget({1,1,1,4,4,4,7,7,7}, x), rawget({1,1,1,4,4,4,7,7,7}, y)
			return false if 9 != #lume.unique([(if 'number' == type @field[j][i] then @field[j][i] else @field[j][i][1]) for j=sy,sy+2 for i=sx,sx+2])
			true
		for y=1,9
			for x=1,9
				if 'number' == type @field[y][x] then continue
				if 1 != #@field[y][x] then return false
				if not isvalid(x, y) then return false
		true

	solve: =>
		ss = solve([lume.clone(row) for row in *stagedata[@id].field])
		@field = [ [(if c>0 then c else {ss[y][x]}) for x, c in ipairs row] for y, row in ipairs stagedata[@id].field]

	update: (dt)=>
		super(dt)
		if @iswin()
			stagedata[@id].cleartime = math.min(@time, stagedata[@id].cleartime or math.maxinteger)
			save()
			@parent\setStageLabels?()
			@@Result()\attach()
		@time += dt
		return love.timer.sleep(2/60) if input.idletime>0 --and 'battery' == (love.system.getPowerInfo()) -- 省エネモード
		if ix, iy = @getIndexPos() -- 取れない場合は nil, nilを受け取る
			if input.MLBp() and 'table' == type @field[iy][ix]
				x, y = love.mouse.getPosition()
				@draggingbox = {ix, iy, 1, 1, value: 0, startpos: {x, y}, offset: if x < 88 then x elseif x > 512-56 then 144-(512-x) else 72}
				@update = @update_dragging
		@keyboard\update(dt)
		-- -- ここからはキーボード操作
		if d = input.arrowkeysp({{-1,0},{0,-1},{1,0},{0,1}}, 10)
			@keycursor = {lume.clamp(@keycursor[1]+d[1], 1, 9), lume.clamp(@keycursor[2]+d[2], 1, 9), visible: true}
		if input.Ap() and 'table' == type @field[@keycursor[2]][@keycursor[1]]
			@draggingbox = {@keycursor[1], @keycursor[2], 1, 1, value: 0, startpos: {@@OX+(@keycursor[1]-.5)*@@W/9, @@OY+(@keycursor[2]-.5)*@@H/9}, offset: 72}
			@keycursor.visible = true
			@update = @update_dragging
		if SoftKeyboard.counters.appback >= 60
			@detach()
			SoftKeyboard.counters.appback =0
		if SoftKeyboard.counters.f9 >= 60
			print lume.time ->@solve()
			@time = 99*60+59
			SoftKeyboard.counters.f9 =0

	update_dragging: (dt)=>
		@time += dt
		@_static_old_value or= 0 -- カチカチ鳴らすため
		if input.MLB()
			x, y = love.mouse.getPosition()
			if @draggingbox.startpos[2] - y > 35
				@draggingbox.value = lume.clamp(math.floor((@draggingbox.offset-(@draggingbox.startpos[1]-x))/16)+1, 1, 9)
			else
				@draggingbox.value = 0
			if @_static_old_value != @draggingbox.value
				play'assets/sounds/効果音ラボ/カードを台の上に出す.mp3'
				@_static_old_value = @draggingbox.value
		for k in *{'1','2','3','4','5','6','7','8','9','kp1','kp2','kp3','kp4','kp5','kp6','kp7','kp8','kp9',}
			if love.keyboard.isDown(k)
				@draggingbox.value = tonumber(k\sub(-1))
		if (input.counters.MLB > 40 or input.counters.A > 40) and #@field[@draggingbox[2]][@draggingbox[1]] > 0 and @draggingbox.value == 0 -- 取り消し
			@field[@draggingbox[2]][@draggingbox[1]] = {}
			play'assets/sounds/効果音ラボ/キャンセル5.mp3'
			@@.CancelWave(@draggingbox[1], @draggingbox[2])
			@draggingbox = nil
			@update = nil
		elseif input.MLBr() or input.Ar() -- 数字を置く、または追加
			if @draggingbox.value != 0
				@field[@draggingbox[2]][@draggingbox[1]][] = @draggingbox.value
				@field[@draggingbox[2]][@draggingbox[1]] = lume.unique @field[@draggingbox[2]][@draggingbox[1]]
				play'assets/sounds/効果音ラボ/決定ボタンを押す42.mp3'
			@draggingbox = nil
			@update = nil

	keypressed: (key, _scancode, _isrepeat)=>
		switch key
			-- when 'appback' then @detach()
			-- when 'f9'
			-- 	ss = solve([lume.clone(row) for row in *stagedata[@id].field])
			-- 	@field = [ [(if c>0 then c else {ss[y][x]}) for x, c in ipairs row] for y, row in ipairs stagedata[@id].field]
			-- 	@time = 99*60+59
			when 'm' then love.audio.toggleMute()

	draw: =>
		aw, ah = @@W/9, @@H/9
		gr.clear(gr.uipalette.base)
		if @update == @update_dragging
			gr.rectangle(gr.setColor(gr.uipalette.base2) or 'fill', @@OX+aw*(@draggingbox[1]-1), @@OY+aw*(@draggingbox[2]-1), aw, ah)
		gr.setColor(gr.uipalette.text)
		for w, s in ipairs {{1, 2, 4, 5, 7, 8}, {3, 6}, {0, 9}}
			gr.setLineWidth(w)
			gr.line(@@OX, @@OY+y*@@H/9, @@OX+@@W, @@OY+y*@@H/9) for y in *s
			gr.line(@@OX+x*@@W/9, @@OY, @@OX+x*@@W/9, @@OY+@@H) for x in *s
		gr.setFont(gr.newFlyweightFont($FONT1, 30))
		for y, row in ipairs @field
			for x, c in ipairs row
				if 'number' == type c
					gr.print_center(gr.setColor(gr.uipalette.text) or gr.setFont(gr.newFlyweightFont($FONT1, 30)) or c, {@@OX+aw*(x-1), @@OY+ah*(y-1), aw, ah})
				else
					switch #c
						when 0 then lume.noop() -- continue でもいけた
						when 1 then gr.print_center(gr.setColor(gr.uipalette.selected) or gr.setFont(gr.newFlyweightFont($FONT1, 30)) or c[1], {@@OX+aw*(x-1), @@OY+ah*(y-1), aw, ah})
						else        gr.print_center(gr.setColor(gr.uipalette.selected) or gr.setFont(gr.newFlyweightFont($FONT1, 18)) or lume.reduce(c,(a,b)->a..b), {@@OX+aw*(x-1), @@OY+ah*(y-1), aw, ah/2})
		if @keycursor.visible then gr.rectangle(gr.setColor(gr.uipalette.text) or 'line', @@OX+aw*(@keycursor[1]-1)+4, @@OY+ah*(@keycursor[2]-1)+4, aw-8, ah-8)
		gr.printf(gr.setColor(gr.uipalette.text) or gr.setFont(gr.newFlyweightFont($FONT1)) or "%s %02d:%02d"\format(L'Time', @time |>idiv(60), @time % 60), @@OX, @@OY-36, @@W, 'right') -- :stopwatch: \u{23F1}
		gr.printf(L'author'..' '..text, @@OX, @@OY+@@H+6, @@W, 'right') if text = stagedata[@id].author
		-- gr.setColor(gr.uipalette.text
		@keyboard\draw()
		if 0 == love.audio.getVolume() then gr.print(gr.setFont(gr.newFlyweightFont($FONT2)) or gr.setColor(gr.uipalette.red) or '\u{1f6ab}', @keyboard.buttons.m[1]-25, @keyboard.buttons.m[2]-25)
		if @update == @update_dragging
			mx, my = love.mouse.getPosition()
			gr.circle(gr.setColor(gr.uipalette.selected) or 'line', mx, my-55, 10)
			-- box = {@@OX+aw*(@draggingbox[1]-1)-@draggingbox.offset, @@OY+aw*(@draggingbox[2]-1)-80, aw+100, 30}
			box = {@draggingbox.startpos[1]-@draggingbox.offset, @draggingbox.startpos[2]-120, 144--[[aw+100]], 30}
			gr.roundrect10(gr.setColor(gr.uipalette.base2) or 'fill', unpack box)
			gr.setScissor(unpack box)
			gr.setFont(gr.newFlyweightFont($FONT1))
			for i=1,9
				gr.setColor(if @draggingbox.value == i then gr.uipalette.selected else gr.uipalette.text)
				gr.print(i, box[1]+(i-1)*16, box[2])
			gr.setScissor()

	class @CancelWave extends Overlay
		new: (x, y)=>
			@box = {Game.OX+(x-1)*Game.W/9, Game.OY+(y-1)*Game.H/9, Game.W/9, Game.H/9}
			@color = {...gr.uipalette.selected, [4]: .7}
			@tween = tween.new(.35, @, {box: align.enlarge(@box, 25), color: {...gr.uipalette.selected,[4]:0}}, 'outQuad')
			super()
		_update: (dt)=>
			while not @tween\update(dt)
				input.idletime = 0
				_, dt = coroutine.yield() -- １回目の呼び出しにselfが必要？なので揃える
		draw: =>
			gr.setColor(@color)
			gr.roundrect10('fill', unpack @box)

	class @Result extends SelectBox
		new: =>
			super({{text: '', box: {30, 0, 452, 300}, command: ()->@parent\detach(), state4index: {} }})
			play 'assets/sounds/効果音ラボ/決定ボタンを押す21.mp3'
		-- predrawback: =>
		-- 	gr.setShader(blur5x5shader)
		-- 	gr.setColor(.5,.5,.5,1)
		draw: =>
			-- gr.setShader()
			-- gr.rectangle(gr.setColor(0,0,0,.1) or 'fill', 0, 0, maid64.sizeX, maid64.sizeY) -- 暗くする(外側まで)
			-- c0, _ = gr.getCanvas(), gr.setCanvas()
			-- gr.rectangle(gr.setColor(0,0,0,.1) or 'fill', 0, 0, gr.getWidth(), gr.getHeight())
			-- gr.setCanvas(c0)
			@_tween1 or= tween.new(.5, @buttons[1].box, {30, 200, 452, 300} ) -- static変数っぽくインスタンス変数を使う。draw内だけでアニメーション
			@_tween1\update(1/60)
			-- gr.rectangle(gr.setColor(gr.uipalette.base) or 'fill', unpack @buttons[1].box)
			-- gr.rectangle(gr.setColor(hsv(love.timer.getTime()*3, .8, .8)) or 'line', unpack @buttons[1].box)
			font0, _ = gr.getFont(), gr.setFont(gr.newFlyweightFont($FONT1, 48))
			gr.print_center(gr.setColor({...gr.uipalette.text, [4]: .5+.5*lume.pingpong(love.timer.getTime())}) or "Clear!!", @buttons[1].box |> align.top(0.6))
			gr.print_center(--[[gr.setColor(gr.uipalette.text) or]] "✗", @buttons[1].box |> align.bottom(0.3))
			gr.setFont(font0)

class Title extends SelectBox
	new: =>
		super(@@.buildButtons({"[→19]ステージ", "[→19][｜]説明[せつめい]"}, align.div_v({10,205,maid64.sizeX-20,36}, 2), [(->) for _=1,2], @@.buildState4Indexes(2, 2)))
		@stagelist = SelectBoxScroll(@@.buildButtons(
			['' for _=1, #stagedata],
			[{0,36*i,470,36} for i=0, #stagedata-1],
			[(-> Game(i)\attach()) for i=1, #stagedata]
		), {21, 260, 470, 470})
		@setStageLabels()
		@keyboard = SoftKeyboard({'appback': {50, 40, 28, '◀'}, 'f9': {120, 30, 20, "撮"--[[\u{1f4f7}]]}, 'f6': {175, 30, 20, "貼"--[[\u{1f4cb}]]}, 'm': {220, 30, 20, "音"}})
		-- @keyboard = SoftKeyboard({'appback': {50, 40, 28, '\u{1f519}'}, 'f9': {120, 30, 20, '\u{1f4f7}'}, 'f6': {175, 30, 20, '\u{1f4cb}'}, 'm': {220, 30, 20, '\u{1f508}'}}, gr.newFlyweightFont($FONT2))
	setStageLabels: =>
		-- @stagelist.buttons[i].text = "[→9]%s #%02d  [@%s;16]作者:[@] %-20s\n[↓-36][→375]%s"\format((if e.cleartime then '[#22cc22]\u{2611}[#]'else'[#444444]\u{2610}[#]'), i, $FONT1(), (if e.author then e.author else ''), (if e.cleartime then '%02d:%02d'\format(e.cleartime/60, e.cleartime % 60) else '--:--')) for i, e in ipairs stagedata
		@stagelist.buttons[i].text = "[→9]%s #%02d  作者: %-20s\n[↓-36][→375]%s"\format((if e.cleartime then '[#22cc22]\u{2611}[#]'else'[#444444]\u{2610}[#]'), i, (if e.author then e.author else ''), (if e.cleartime then '%02d:%02d'\format(e.cleartime/60, e.cleartime % 60) else '--:--')) for i, e in ipairs stagedata
	update: (dt)=>
		-- return if input.idletime>0 --and 'battery' == (love.system.getPowerInfo()) -- 省エネモード
		super(dt)
		if 1 == @index
			@stagelist\update(dt)
		@keyboard\update(dt)
	wheelmoved: (x, y)=>
		super(x, 0) -- チルトホイールマウス持ってないが
		@stagelist\wheelmoved(0, y)
	keypressed: (key, scancode, isrepeat)=>
		super(key, scancode, isrepeat)
		-- if 1 == @index -- tab/s-tabはstagelistに伝播させない
		-- 	@stagelist\keypressed(key, scancode, isrepeat)
		switch key
			when 'appback' then lume.noop()--@detach()
			when 'f9' then @@.AutoScreenshot()
			when 'm' then love.audio.toggleMute()
	filedropped: (file)=>
		if DEBUGMODE
			file\open('r')
			content = file\read()
			game = Game(1)\attach()
			game.field = [(if 0x20==c then 0 else c-0x30) for _, c in utf8.codes lume.split(content)] -- authorが変更できないがこのままでもいいか
	draw: =>
		maid64.clearBG()
		-- gr.draw(gr.setColor(1,1,1) or gr.newFlyweightImage('assets/shikaku.jpg'), 10, 80, 0, 0.8)
		-- gr.setFont(gr.newFlyweightFont($FONT1)) --24
		gr.setColor(gr.uipalette.text)
		@keyboard\draw()
		if 0 == love.audio.getVolume() then gr.print(gr.setFont(gr.newFlyweightFont($FONT2)) or gr.setColor(gr.uipalette.red) or '\u{1f6ab}', @keyboard.buttons.m[1]-25, @keyboard.buttons.m[2]-25)
		gr.setFont(gr.newFlyweightFont($FONT1)) --24
		super()
		if 1 == @index
			@stagelist\draw()
		elseif 2 == @index
			gr.rectangle(gr.setColor(gr.uipalette.base2) or 'fill', 31, 260, 450, 500)
			gr.setColor(gr.uipalette.text)
			gr.draw_text("[#{sweetie16.<>.Blue}]ルール[#]
① 点線の上にタテヨコに線を引き、[｜]盤面[ばんめん]を
いくつかの[｜]長方形[ちょうほうけい]に分けましょう。
② どの長方形にも数字が一つずつ入る
ようにします。
③ 数字は１マスの面積を１としたとき
の面積を表しています。
[#{sweetie16.<>.Blue}]操作方法[#]
", 31, 260)


	class @AutoScreenshot extends Overlay
		_update: (_)=>
			for i = 1, #stagedata
				Game(i)\attach() -- stagelistからgui-automationでやるのは出来なかった
				-- love.keypressed('f9')
				time = 1.5
				while time > 0
					_, dt = coroutine.yield()
					time -= dt
				love.keypressed('appback')

class Title2 extends SelectBox
	new: =>
		boxes = do
			s = align.div_h({16, 200, maid64.sizeX-32, 100}, 2)
			[align.enlarge(b, -4, -4) for b in *{...align.div_v(s[1], 3), ...align.div_v(s[2], 3)}]
		super(@@.buildButtons({'Level1', 'Level2', 'Level3', 'Level4', 'Level5', 'Level6'}, boxes, {(->@generate(30)), (->@generate(40)), (->@generate(45)), (->@generate(50)), (->@generate(55)), (->@generate(58)) }))
	update: (dt)=>
		super(dt)
		-- if nil == love.window.fullscreen_offset_x and nil == love.window.fullscreen_offset_y and love.window.getFullscreen() and 'Web' == love.system.getOS()
		-- 	w, h = gr.getDimensions()
		-- 	sw, sh = love.window.getDesktopDimensions()
		-- 	if w/sh < h/sh then love.window.fullscreen_offset_x = -(sw - w) / 2
		-- 	else                love.window.fullscreen_offset_y = -(sh - h) / 2
		-- elseif not love.window.getFullscreen()
		-- 	love.window.fullscreen_offset_x, love.window.fullscreen_offset_y = nil, nil

	generate: (n)=>
		with Game(1)
			.field = [ [(if c>0 then c else {})for c in *row] for row in *generate(n)]
			\attach()
	draw: =>
		gr.print(gr.setColor(gr.uipalette.text) or gr.setFont(gr.newFlyweightFont($FONT1, 100)) or '数独', 140, 20)
		gr.setFont(gr.newFlyweightFont($FONT1), 40)
		super()
		gr.print "#{love.system.getOS()} #{if love.window.getFullscreen() then 'fullscreen' else 'not fullscreen'}
Desktop: #{love.window.getDesktopDimensions()} #{select 2, love.window.getDesktopDimensions()}
Window : #{gr.getWidth()}, #{gr.getHeight()}
Original: #{love.mouse.getX()}, #{love.mouse.getY()}
Scaled:  #{love.mouse.getPosition()}, #{select 2, love.mouse.getPosition()}
Offset: #{love.window.fullscreen_offset_x}, #{love.window.fullscreen_offset_y}", 20, 400

love.draw = -> -- 省エネモード(アニメーションの毎にinput.idletime=0を挟まなければいけないのでGameシーンのみに限定する)
	self = Scene.current
	return love.draw1() if @@__name != 'Game' or input.idletime < 0.1 --: or 'battery' != (love.system.getPowerInfo())
	maid64.clearBG()
	-- if @time
	gr.setCanvas(maid64.canvas) -- 時計の部分だけ更新
	gr.rectangle(gr.setColor(gr.uipalette.base) or 'fill', @@OX+300, @@OY-36, 500-300, 30)
	gr.printf(gr.setFont(gr.newFlyweightFont($FONT1)) or gr.setColor(gr.uipalette.text) or"%s %02d:%02d"\format(L'Time', @time |>idiv(60), @time % 60), @@OX, @@OY-36, 500, 'right')
	maid64.finish()


love.load = ->
	Title2()\attach()

	if DEBUGMODE -- hotswap
		Watchdog{
			"#{$FILE}": -> transpile("#{$FILE}", 'main.lua')
			...Watchdog.InitialTargets}
	if DEBUGMODE -- build
		-- os.execute 'rustc -C opt-level=3 --crate-type=cdylib ray.rs' -- android(32bit?)用
		-- os.execute 'rustc -C opt-level=3 --crate-type=cdylib ray.rs' -- Windows用
		-- os.execute 'zig build-lib -dynamic -target arm-linux-gnueabi ray.zig' -- android(32bit?)用
		-- os.execute 'zig build-lib -dynamic -target x86_64-windows ray.zig' -- Windows用
		love.thread.newThread([[
_G.PROJECTNAME, _G.VERSION = ...
require('distribute').build{'sudoku_stagedata.lua', '数独.lua'}
]])\start(PROJECTNAME, VERSION) -- 遅いので別スレッドにした。utlからlume.luaへ書き写したものあり。一行の時ファイル名複数行の時コードと判断してるようだ
